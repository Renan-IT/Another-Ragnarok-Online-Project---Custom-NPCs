//===== eAthena Script =======================================
//= Build Manager +
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.8
//===== Description: =========================================
//= Stores stat builds into a single character variable.
//============================================================

izlude,126,148,6	script	Build Manager	4_F_NURSE,{
function Get_Menu; function Save_Build; function Load_Build;

	set .@BuildCount,4;	// Number of builds to use
	set .@Save,2000;	// Zeny required to save a build
	set .@Load,2000;	// Zeny required to load a build
	set .@Rename,500;	// Zeny required to rename a build
	set .@MaxLevel,99;	// Maximum base level (to prevent stat overflow)

	if (BaseLevel > .@MaxLevel) { mes "You must be level "+.@MaxLevel+" to use this."; end; }
	switch(select("Sauvegarder Build:Charger Build:Renommer Build:Fermer")) {
		case 1:
			if (.@Save) mes "Sauvegarder un Build coûte "+.@Save+" Zeny.";
			set .@Build, Get_Menu(.@BuildCount);
			if (Zeny<.@Save) { mes "Pas assez de Zeny."; close; }
			if (getd("Build_"+.@Build+"$")!="") {
				mes "Voulez vous écraser le précédent Build #"+.@Build+"?";
				if(select("Sauvegarder le nouveau Build:Fermer")==2) close; }
			Save_Build(.@Build);
			mes "Entrez un nom pour le Build.";
			input getd("Build_"+.@Build+"n$");
			mes "Le Build #"+.@Build+" ("+getd("Build_"+.@Build+"n$")+") a été sauvegardé.";
			set Zeny, Zeny-.@Save;
			close;
		case 2: 
			if (.@Load) mes "Charger un Build coûte "+.@Load+" Zeny.";
			set .@Build, Get_Menu(.@BuildCount);
			if (getd("Build_"+.@Build+"$")=="") {
				mes "Aucun Build trouvé."; close; }
			if (Zeny<.@Load) { mes "Pas assez de Zeny."; close; }
			Load_Build(getd("Build_"+.@Build+"$"));
			mes "Le Build #"+.@Build+" a été chargé.";
			set Zeny, Zeny-.@Load;
			close;
		case 3:
			if (.@Rename) mes "Renommer un Build coûte "+.@Rename+" Zeny.";
			set .@Build, Get_Menu(.@BuildCount);
			if (getd("Build_"+.@Build+"$")=="") {
				mes "Aucun Build trouvé."; close; }
			if (Zeny<.@Rename) { mes "Pas assez de Zeny."; close; }
			mes "Entrez un nouveau nom pour le Build #"+.@Build+" ("+getd("Build_"+.@Build+"n$")+").";
			input getd("Build_"+.@Build+"n$");
			mes "Le Build #"+.@Build+" a été renommé.";
			set Zeny, Zeny-.@Rename;
			close;
		case 4:
			close; }
	function Get_Menu {
		set .@menu$,"";
		for(set .@i,1; .@i<=getarg(0); set .@i,.@i+1)
			set .@menu$, .@menu$+"Slot "+.@i+" ("+((getd("Build_"+.@i+"n$")=="")?"^777777libre":"^0055FF"+getd("Build_"+.@i+"n$"))+"^000000):";
		return select(.@menu$); }
	function Save_Build {
		set .@s$,"";
		for(set .@i,13; .@i<19; set .@i,.@i+1)
			set .@s$,.@s$+readparam(.@i)+"|";
		setd "Build_"+getarg(0)+"$", .@s$+StatusPoint;
		return; }
	function Load_Build {
		resetstatus;
		explode(.@s$,getarg(0),"|");
		for(set .@i,0; .@i<6; set .@i,.@i+1)
			statusup2 (.@i+13), atoi(.@s$[.@i])-1;
		set StatusPoint, atoi(.@s$[6]);
		return; }
}
